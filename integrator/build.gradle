plugins {
    id 'org.springframework.boot' version '2.4.1'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'java'
}

group = 'org.bahmni_avni_integration'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '15'

repositories {
    mavenCentral()
    maven {
        url "http://repo.mybahmni.org.s3.amazonaws.com/artifactory/snapshot"
    }
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

tasks.withType(JavaCompile).all {
    options.compilerArgs += ['--enable-preview']
}

tasks.withType(Test).all {
    jvmArgs += '--enable-preview'
}

tasks.withType(JavaExec) {
    jvmArgs += '--enable-preview'
}

task migrateTestDb() {
    dependsOn ":integration-data:migrateTestDb"
}

task migrateDb() {
    dependsOn ":integration-data:migrateDb"
}

dependencies {
    implementation project(':integration-data')
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-rest'
    implementation 'org.springframework.boot:spring-boot-starter-quartz'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'org.postgresql:postgresql'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    compile group: 'org.ict4h', name: 'atomfeed-client', version: '1.9.4'
    compile group: 'org.ict4h', name: 'atomfeed-spring-server', version: '1.9.4'
    implementation platform('software.amazon.awssdk:bom:2.15.0')
    implementation 'software.amazon.awssdk:cognitoidentity'
    implementation 'com.bugsnag:bugsnag-spring:3.6.4'

    runtimeOnly group: 'mysql', name: 'mysql-connector-java', version: '5.1.6'

    implementation platform('com.amazonaws:aws-java-sdk-bom:1.11.939')
    implementation 'com.amazonaws:aws-java-sdk-cognitoidp'
    compile group: 'org.json', name: 'json', version: '20201115'
    compile "log4j:log4j:1.2.16"
    compile "commons-io:commons-io:2.4"
    compile "commons-lang:commons-lang:2.6"

    compile "org.javatuples:javatuples:1.2"

    implementation 'com.bugsnag:bugsnag-spring:3.6.4'

    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.security:spring-security-test'
}

sourceSets {
    main {
        resources {
            srcDir '../integration-data/src/main/resources'
        }
    }
    test {
        resources {
            srcDir '../integration-data/src/main/resources'
        }
    }
}

task unitTest( type: Test ) {
    useJUnitPlatform()
    exclude ("**/**ExternalTest.class")
    exclude ("**/**FullRunTest.class")
}

test {
    useJUnitPlatform()
    include ("**/**ExternalTest.class")
    exclude ("**/**FullRunTest.class")
}
